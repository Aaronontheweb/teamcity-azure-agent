def versionNumber = System.getenv('versionNumber') != null ? System.getenv('versionNumber') :  'SNAPSHOT'
ext.projectIds = ['group': 'teamcity-azure-plugin', 'version': versionNumber, 'artifact': 'cloud-azure' ]

group = projectIds.group
version = projectIds.version

allprojects {
    group = projectIds.group
    version = projectIds.version
}

subprojects {
    apply plugin: 'java'
    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    repositories {
        mavenCentral()
        maven { url 'http://repository.jetbrains.com/all' }
        maven { url 'http://adxsnapshots.azurewebsites.net/' }
        maven { url '../local-repo' }
    }

    test.useTestNG()
    jar.version = null
}

project(':plugin-azure-agent') {
    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    dependencies {
        compile project(':plugin-azure-common')
        compile 'org.jetbrains.teamcity:agent-api:9.0'
        compile 'org.jetbrains.teamcity.internal:agent:9.0'
        compile 'org.jetbrains.teamcity:cloud-interface:9.1.5'
        compile 'org.jetbrains.teamcity:cloud-shared:9.1.5'
        runtime 'javax.servlet:jstl:1.1.2'
        testCompile 'org.testng:testng:6.8'
    }
    
    task distrib(type: Zip, dependsOn: jar) {
        archiveName = projectIds.artifact + '.zip'
    
        from (libsDir) {
            into 'lib'
        }
        
        from (configurations.compile) {
            include 'plugin-azure-*.jar'
            into 'lib'
        }
        
        from 'teamcity-plugin.xml'
        
        into projectIds.artifact
    }
    
    artifacts {
        archives distrib
    }
}

project(':plugin-azure-common') {
    sourceCompatibility = 1.6
    targetCompatibility = 1.6
}

project(':plugin-azure-server-base') {
    dependencies {
        compile project(':plugin-azure-common')
        compile 'org.jetbrains.teamcity:server-api:9.0'
        compile 'org.jetbrains.teamcity:cloud-interface:9.1.5'
        compile 'org.jetbrains.teamcity:cloud-shared:9.1.5'
        testCompile 'org.testng:testng:6.8'
    }
}

project(':plugin-azure-server-asm') {
    dependencies {
        compile project(':plugin-azure-server-base')
        compile 'org.jetbrains.teamcity:cloud-server:9.1.5'
        compile 'org.jetbrains.teamcity:web:9.1.5'
        compile 'com.microsoft.azure:azure-core:0.9.1'
        compile 'com.microsoft.azure:azure-svc-mgmt:0.9.1'
        compile 'com.microsoft.azure:azure-svc-mgmt-compute:0.9.1'
        compile 'com.microsoft.azure:azure-svc-mgmt-network:0.9.1'
        compile 'org.bouncycastle:bcprov-jdk15on:1.54'
        runtime 'javax.servlet:jstl:1.1.2'
        testCompile 'org.testng:testng:6.8'
    }
    
    task distrib(type: Zip, dependsOn: [':plugin-azure-agent:assemble', jar]) {
        archiveName = projectIds.artifact + '-asm.zip'
    
        from (libsDir) {
            into 'server'
        }
        
        from (configurations.compile) {
            include 'activation-*.jar'
            include 'azure-*.jar'
            include 'bcprov-*.jar'
            include 'cloud-azure-*.jar'
            include 'commons-cli-*.jar'
            include 'commons-codec-*.jar'
            include 'commons-lang-*.jar'
            include 'commons-logging-*.jar'
            include 'gson-*.jar'
            include 'http*.jar'
            include 'jackson-*.jar'
            include 'javax*.jar'
            include 'jaxb-*.jar'
            include 'jersey-*.jar'
            include 'jettison-*.jar'
            include 'jsch-*.jar'
            include 'mail-*.jar'
            include 'plugin-azure-*.jar'
            include 'stax-api-*.jar'
            into 'server'
        }
        
        from (project(':plugin-azure-agent').file('build/distributions')) {
            include projectIds.artifact + '.zip'
            into 'agent'
        }
        
        from ('teamcity-plugin.xml') {
            filter {
                it.replaceAll('@Version@', projectIds.version)
            }
        }        
    }
    
    artifacts {
        archives distrib
    }
}

project(':plugin-azure-server-arm') {
    dependencies {
        compile project(':plugin-azure-server-base')
        compile 'org.jetbrains.teamcity:cloud-server:9.1.5'
        compile 'org.jetbrains.teamcity:web:9.1.5'
        compile 'org.jetbrains.teamcity:server:9.1.5'
        compile 'com.microsoft.azure:azure-mgmt-compute:1.0.0-SNAPSHOT'
        compile 'com.microsoft.azure:azure-mgmt-network:1.0.0-SNAPSHOT'
        compile 'com.microsoft.azure:azure-mgmt-resources:1.0.0-SNAPSHOT'
        compile 'com.microsoft.azure:azure-mgmt-storage:1.0.0-SNAPSHOT'
        compile 'com.microsoft.azure:azure-client-authentication:1.0.0-SNAPSHOT'
        compile 'com.microsoft.azure:azure-client-runtime:1.0.0-SNAPSHOT'
        compile 'com.microsoft.azure:azure-storage:4.0.0'
        compile 'org.jdeferred:jdeferred-core:1.2.4'
        runtime 'javax.servlet:jstl:1.1.2'
        testCompile 'org.testng:testng:6.8'
    }
    
    task distrib(type: Zip, dependsOn: [':plugin-azure-agent:assemble', jar]) {
        archiveName = projectIds.artifact + '-arm.zip'
    
        from (libsDir) {
            into 'server'
        }
        
        from (configurations.compile) {
            include 'azure-*.jar'
            include '*client-runtime-*.jar'
            include 'adal4j-*.jar'
            include 'lang-tag-*.jar'
            include 'nimbus-*.jar'
            include 'oauth2-*.jar'
            include 'json-smart-*.jar'
            include 'commons-codec-1.10.jar'
            include 'commons-lang3-*.jar'
            include 'converter-jackson-*.jar'
            include 'jackson-*.jar'
            include 'logging-interceptor-*.jar'
            include 'ok*.jar'
            include 'plugin-azure-*.jar'
            include 'retrofit-*.jar'
            include 'guava-18*.jar'
            include 'jdeferred-core-*.jar'
            into 'server'
        }
        
        from (project(':plugin-azure-agent').file('build/distributions')) {
            include projectIds.artifact + '.zip'
            into 'agent'
        }
        
        from ('teamcity-plugin.xml') {
            filter {
                it.replaceAll('@Version@', projectIds.version)
            }
        }        
    }
    
    artifacts {
        archives distrib
    }
}