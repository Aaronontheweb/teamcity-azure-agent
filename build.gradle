buildscript {
    ext {
        kotlin_version = '1.1.0-beta-38'
        kotlinx_version = '0.8-beta'
    }
    repositories {
        mavenCentral()
        maven {
            url "https://dl.bintray.com/kotlin/kotlin-eap-1.1/"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'com.github.rodm.teamcity-server' version '0.9.1'
}

ext {
    def correctVersion = project.hasProperty('versionNumber') && property('versionNumber') ==~ /\d+\.\d+\.\d+.*/
    versionNumber = correctVersion ? property('versionNumber') : 'SNAPSHOT-' + new Date().format('yyyyMMddHHmmss')
    projectIds = ['group': 'teamcity-azure-plugin', 'version': versionNumber]
    teamcityVersion = findProperty('teamcityVersion') ?: '10.0'
}

group = projectIds.group
version = projectIds.version

allprojects {
    group = projectIds.group
    version = projectIds.version
}

subprojects {
    apply plugin: 'java'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    test.useTestNG()
    jar.version = null
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14.1'
}